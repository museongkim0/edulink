pipeline {
    agent any

    options {
        timestamps()
    }

    environment {
        IMAGE_NAME = 'mstar228/edulink-backend'
        IMAGE_TAG = "1.0.${BUILD_NUMBER}"
        KUBE_HOST = 'test@192.0.2.7'
        PATH = "/usr/local/bin:${env.PATH}"
    }

    stages {
        stage('Start Timer') {
            steps {
                script {
                    startTime = System.currentTimeMillis()
                }
            }
        }

        stage('Git Clone') {
            steps {
                echo "Cloning Repository"
                git url: 'https://github.com/beyond-sw-camp/be12-4th-404Error-EduLink.git',
                    branch: "${env.GIT_BRANCH.replace('origin/', '')}"
            }
            post {
                success {
                    sendDiscordMessage("✅ Git Clone 성공!", "Git Repository 클론 완료", "GREEN")
                }
                failure {
                    sendDiscordMessage("❌ Git Clone 실패!", "Git Repository 클론 중 오류 발생", "RED")
                }
            }
        }

        stage('Gradle Build') {
            steps {
                echo "Building Project"
                sh '''
                    echo "Add Permission"
                    chmod +x ${WORKSPACE}/backend/gradlew
                    echo "Cleaning previous build"
                    rm -rf ${WORKSPACE}/backend/build
                    echo "Running Gradle build"
                    cd ${WORKSPACE}/backend && ./gradlew bootJar
                '''
            }
            post {
                success {
                    sendDiscordMessage("✅ Gradle Build 성공!", "Spring Boot 빌드 완료", "GREEN")
                }
                failure {
                    sendDiscordMessage("❌ Gradle Build 실패!", "Spring Boot 빌드 중 오류 발생", "RED")
                }
            }
        }

        stage('Build Docker Image') {
            steps {
                script {
                    echo "Building Docker Image ${IMAGE_NAME}:${IMAGE_TAG}"
                    docker.build("${IMAGE_NAME}:${IMAGE_TAG}", "${WORKSPACE}/backend")
                }
            }
            post {
                success {
                    sendDiscordMessage("✅ Docker Build 성공!", "Docker 이미지 빌드 완료", "GREEN")
                }
                failure {
                    sendDiscordMessage("❌ Docker Build 실패!", "Docker 이미지 빌드 중 오류 발생", "RED")
                }
            }
        }

        stage('Push to Registry') {
            steps {
                script {
                    echo "Pushing Docker Image ${IMAGE_NAME}:${IMAGE_TAG} to Registry"
                    withDockerRegistry([credentialsId: 'DOCKER_HUB']) {
                        docker.image("${IMAGE_NAME}:${IMAGE_TAG}").push()
                        docker.image("${IMAGE_NAME}:${IMAGE_TAG}").push('latest')
                    }
                }
            }
            post {
                success {
                    sendDiscordMessage("✅ Docker Push 성공!", "Docker 이미지 푸시 완료", "GREEN")
                }
                failure {
                    sendDiscordMessage("❌ Docker Push 실패!", "Docker 이미지 푸시 중 오류 발생", "RED")
                }
            }
        }

        stage('Determine Deploy Color') {
            steps {
                script {
                    try {
                        // Query the current service selector to see which deployment is active.
                        def svcSelector = sh(
                            script: "ssh ${KUBE_HOST} 'kubectl get svc backend-svc -o jsonpath=\"{.spec.selector.app}\"'",
                            returnStdout: true
                        ).trim()
                        echo "Current svc selector: ${svcSelector}"

                        if (svcSelector.contains("green")) {
                            // Service is pointing to green; new deployment should be blue.
                            env.DEPLOY_COLOR = 'blue'
                            env.OTHER_COLOR = 'green'
                        } else if (svcSelector.contains("blue")) {
                            // Service is pointing to blue; new deployment should be green.
                            env.DEPLOY_COLOR = 'green'
                            env.OTHER_COLOR = 'blue'
                        } else {
                            // Fallback: Check ready replicas if the svc selector is not as expected.
                            def blueReady = sh(
                                script: "ssh ${KUBE_HOST} 'kubectl get deployment backend-deployment-blue -o jsonpath=\"{.status.readyReplicas}\"' || echo '0'",
                                returnStdout: true
                            ).trim()
                            def greenReady = sh(
                                script: "ssh ${KUBE_HOST} 'kubectl get deployment backend-deployment-green -o jsonpath=\"{.status.readyReplicas}\"' || echo '0'",
                                returnStdout: true
                            ).trim()
                            echo "Blue ready replicas: ${blueReady}"
                            echo "Green ready replicas: ${greenReady}"

                            if (blueReady == '0' && greenReady != '0') {
                                env.DEPLOY_COLOR = 'blue'
                                env.OTHER_COLOR = 'green'
                            } else if (blueReady != '0' && greenReady == '0') {
                                env.DEPLOY_COLOR = 'green'
                                env.OTHER_COLOR = 'blue'
                            } else {
                                // If ambiguous, default to deploying blue.
                                env.DEPLOY_COLOR = 'blue'
                                env.OTHER_COLOR = 'green'
                            }
                        }
                        echo "Deploying ${env.DEPLOY_COLOR} version (scaling down ${env.OTHER_COLOR} later)"
                    } catch (Exception e) {
                        echo "Error determining deploy color: ${e.getMessage()}"
                        // On error, default to blue deployment.
                        env.DEPLOY_COLOR = 'blue'
                        env.OTHER_COLOR = 'green'
                    }
                }
            }
            post {
                success {
                    sendDiscordMessage("✅ 배포 색상 결정 완료!", "${env.DEPLOY_COLOR} 버전으로 배포 진행", "GREEN")
                }
                failure {
                    sendDiscordMessage("❌ 배포 색상 결정 실패!", "색상 결정 중 오류 발생", "RED")
                }
            }
        }

        stage('Deploy Blue-Green') {
            when {
                expression { env.GIT_BRANCH == 'origin/main' }
            }
            steps {
                script {
                    def deployColor = env.DEPLOY_COLOR ?: 'blue'
                    def otherColor = env.OTHER_COLOR ?: 'green'

                    echo "Deploying ${deployColor} version"

                    // 1. 배포 파일의 이미지 태그 업데이트 후 전송
                    sh """
                        sed -i "s/latest/${IMAGE_TAG}/g" ${WORKSPACE}/backend/k8s/backend-deployment-${deployColor}.yml
                    """
                    sh """
                        scp ${WORKSPACE}/backend/k8s/backend-deployment-${deployColor}.yml ${KUBE_HOST}:/home/test/backend/k8s/
                    """

                    // 2. 새 배포 적용 및 스케일 업
                    sh """
                        ssh ${KUBE_HOST} 'kubectl apply -f /home/test/backend/k8s/backend-deployment-${deployColor}.yml'
                        ssh ${KUBE_HOST} 'kubectl scale deployment backend-deployment-${deployColor} --replicas=2'
                    """

                    // 3. 새 배포 롤아웃 상태 체크
                    sh """
                        ssh ${KUBE_HOST} 'kubectl rollout status deployment/backend-deployment-${deployColor} --timeout=5m'
                    """

                    // 4. 서비스 YAML 파일 내 selector 업데이트
                    // 기존 "app: backend-..." 패턴 전체를 새로운 배포 색상으로 교체
                    sh """
                        sed -i "s/app: backend-.*\$/app: backend-${deployColor}/" ${WORKSPACE}/backend/k8s/backend-svc.yml
                    """
                    sh """
                        scp ${WORKSPACE}/backend/k8s/backend-svc.yml ${KUBE_HOST}:/home/test/backend/k8s/
                    """
                    sh """
                        ssh ${KUBE_HOST} 'kubectl apply -f /home/test/backend/k8s/backend-svc.yml'
                    """

                    // 5. 기존 배포(다른 색상)의 스케일 다운
                    sh """
                        ssh ${KUBE_HOST} 'kubectl scale deployment backend-deployment-${otherColor} --replicas=0'
                    """
                }
            }
            post {
                success {
                    sendDiscordMessage("✅ Blue-Green 배포 성공!", "${env.DEPLOY_COLOR} 버전 배포 완료", "GREEN")
                }
                failure {
                    sendDiscordMessage("❌ Blue-Green 배포 실패!", "배포 중 오류 발생", "RED")
                }
            }
        }

        stage('End Timer') {
            steps {
                script {
                    def endTime = System.currentTimeMillis()
                    def duration = (endTime - startTime) / 1000
                    echo "⏱️ 전체 파이프라인 실행 시간: ${duration}초"
                }
            }
        }
    }

    post {
        success {
            script {
                def endTime = System.currentTimeMillis()
                def duration = (endTime - startTime) / 1000
                sendDiscordMessage("🎉 전체 파이프라인 성공!", "⏱️ 실행 시간: ${duration}초", "GREEN")
            }
        }
        failure {
            script {
                def endTime = System.currentTimeMillis()
                def duration = (endTime - startTime) / 1000
                sendDiscordMessage("🚨 전체 파이프라인 실패!", "⏱️ 실행 시간: ${duration}초", "RED")
            }
        }
    }
}

// 전역 함수 선언 (pipeline 외부)
def sendDiscordMessage(String title, String description, String color) {
    withCredentials([string(credentialsId: 'Discord-Webhook', variable: 'DISCORD')]) {
        discordSend(
            description: description,
            footer: "Jenkins CI/CD",
            link: env.BUILD_URL,
            result: currentBuild.currentResult,
            title: title,
            color: color,
            webhookURL: "$DISCORD"
        )
    }
}
